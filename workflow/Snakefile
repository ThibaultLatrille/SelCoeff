import os
import sys
import pandas as pd

sys.path.append(".")
from scripts.libraries import CategorySNP

configfile: "config/config.yaml"
pop2sp = {}
SFS_LIST, LIST_SUPP, VCF_LIST, MASK_LIST = [], [], [], []
FOLDER = os.path.abspath(".")
FASTA_FOLDER = f"{FOLDER}/data_div/omm_NT_fasta.v10c_116"
TREE_FOLDER = f"{FOLDER}/data_div/omm_RooTree.v10b_116"
EXP_FOLDER = f"{FOLDER}/experiments/{config['experiment']}"
os.makedirs(EXP_FOLDER,exist_ok=True)
sample_list = f"{FOLDER}/config/{config['SAMPLE_LIST']}"
model = config['MODEL']
assert model in ["polyDFE_D", "polyDFE_C", "polyDFE_B"]
USE_DIVERGENCE = "USE_DIVERGENCE" in config and config['USE_DIVERGENCE']
if USE_DIVERGENCE:
    assert config['ALPHA']
polyDFE_param = f"-m {model.split('_')[-1]}"
polyDFE_param += " 6" if model == "polyDFE_D" else ""
polyDFE_param += " -w" if not USE_DIVERGENCE else ""
bins = config['bins']
windows = config['windows']
ontology_subset = ""
method_cats = {m: CategorySNP(m,bound_file="",bins=bins,windows=windows).all() for m in config['METHOD_LIST']}
binning = (f"bins{bins}" if windows == 0 else f"windows{bins}x{windows}")
if 'mask_adaptation' in config and config['mask_adaptation']:
    binning += "NonAdaptive"
    MASK_LIST.append(f"{FOLDER}/data_processed/Mask/adaptation.tsv.gz")
if "mask_identity" in config and config['mask_identity'] > 0.0:
    binning += f"Mask{config['mask_identity']}"
    MASK_LIST.append(f"{FOLDER}/data_processed/Mask/identity.{config['mask_identity']}.tsv.gz")
if "ontology_subset" in config and config['ontology_subset'] != "":
    ontology_subset = config['ontology_subset']
    binning += ontology_subset
    MASK_LIST.append(f"{FOLDER}/data_processed/Mask/Ontology-{ontology_subset}.tsv.gz")
if "mask_unaligned_flanks" in config and config['mask_unaligned_flanks'] > 0:
    binning += f"MaskFlanks{config['mask_unaligned_flanks']}"
if "mask_CpG" in config and config['mask_CpG']:
    binning += "MaskCpG"

output_supp = "OUTPUT_SUPP" in config and config['OUTPUT_SUPP']
for row_id, row in pd.read_csv(sample_list,sep="\t").iterrows():
    pop2sp[row['SampleName']] = row['Species']
    if output_supp:
        LIST_SUPP.append(f"{FOLDER}/TSV/subs.{row['Species']}.{row['SampleName']}.{binning}.pdf")
        LIST_SUPP.append(f"{FOLDER}/TSV/poly.{row['Species']}.{row['SampleName']}.{binning}.pdf")
    vcf = f"{FOLDER}/data_poly/{row['Species']}.{row['SampleName']}.vcf.gz"
    if os.path.exists(vcf):
        VCF_LIST.append(vcf)
    for method in config['METHOD_LIST']:
        SFS_LIST.append(f"{EXP_FOLDER}/analysis/{row['Species']}.{row['SampleName']}.{method}")

if output_supp:
    LIST_SUPP.append(f"{FOLDER}/data_processed/upset_plot/{binning}.pdf")
    LIST_SUPP.append(f"{FOLDER}/data_processed/SNP_effect/{binning}_clinically/output.tsv")
    LIST_SUPP.append(f"{FOLDER}/data_processed/experimental_DFE.pdf")
    LIST_SUPP.append(f"{FOLDER}/data_processed/population_DFE.pdf")

wildcard_constraints:
    # constrain to only alphanumeric characters and underscore
    popu=r"[a-zA-Z_]+",species=r"[a-zA-Z_]+",method=r"[a-zA-Z]+"

rule all:
    input:
        f"{FOLDER}/data_processed/ontology.tsv",
        f"{FOLDER}/data_processed/merged_opportunities/{binning}.tsv",
        f"{FOLDER}/data_processed/merged_bounds/{binning}.tsv",
        f"{EXP_FOLDER}/supp-mat.pdf",
        f"{FOLDER}/data_processed/trimmed_tree_{config['SAMPLE_LIST']}.tree",
        LIST_SUPP


rule trimmed_tree:
    input:
        script=f"{FOLDER}/scripts/trim_tree.py",
        tree=f"{FOLDER}/data_div/mammals_species.nwk",
        sample=sample_list
    output:
        nwk=f"{FOLDER}/data_processed/trimmed_tree_{config['SAMPLE_LIST']}.tree"
    shell:
        "python3 {input.script} --sample {input.sample} --tree {input.tree} --output {output.nwk}"

rule experimental_DFE:
    input:
        script=f"{FOLDER}/scripts/experimental_DFE.py",
        folder=f"{FOLDER}/data_div/distribution_fitness_effects"
    output:
        pdf=f"{FOLDER}/data_processed/experimental_DFE.pdf"
    shell:
        "python3 {input.script} --exp_folder {input.folder} --output {output.pdf}"

rule population_DFE:
    input:
        script=f"{FOLDER}/scripts/population_DFE.py"
    output:
        pdf=f"{FOLDER}/data_processed/population_DFE.pdf"
    shell:
        "python3 {input.script} --output {output.pdf}"

rule fixed_snp_by_pop:
    input:
        script=f"{FOLDER}/scripts/fixed_snp_by_pop.py",
        vcf=f"{FOLDER}/data_poly/{{species}}.{{popu}}.vcf.gz",
        fasta_folder=FASTA_FOLDER
    output:
        fasta=f"{FOLDER}/data_processed/fasta/{{species}}.{{popu}}.fasta.gz"
    shell:
        "python3 {input.script} --species {wildcards.species} --vcf {input.vcf}  --fasta_folder {input.fasta_folder} --output {output.fasta}"

rule SIFT_annotate:
    input:
        script=f"{FOLDER}/scripts/annotate_SIFT.py",
        fasta_folder=FASTA_FOLDER,
        fasta_pop=rules.fixed_snp_by_pop.output.fasta,
        sift_exec=f"{FOLDER}/utils/sift6.2.1/bin/info_on_seqs",
        blimps_dir=f"{FOLDER}/utils/sift6.2.1/blimps"
    output:
        directory(f"{FOLDER}/data_processed/sift/{{species}}.{{popu}}")
    shell:
        "python3 {input.script} --species {wildcards.species} --sift_exec {input.sift_exec} --blimps_dir {input.blimps_dir} --fasta_folder {input.fasta_folder} --fasta_pop {input.fasta_pop} --output {output}"

rule merge_SIFT:
    input:
        script=f"{FOLDER}/scripts/merge_SIFT.py",
        sift_folder=rules.SIFT_annotate.output,
        vcf=f"{FOLDER}/data_poly/{{species}}.{{popu}}.vcf.gz"
    output:
        f"{FOLDER}/data_processed/sift_snps/{{species}}.{{popu}}.tsv"
    shell:
        "python3 {input.script} --species {wildcards.species} --vcf {input.vcf} --sift_folder {input.sift_folder} --output {output}"

rule table_ontology:
    input:
        script=f"{FOLDER}/scripts/table_ontology.py",
        folder=f'{FOLDER}/data_div/Experiments',
        xml=f'{FOLDER}/data_div/omm_markers'
    output:
        tsv=f"{FOLDER}/data_processed/ontology.tsv"
    shell:
        "python3 {input.script} --xml {input.xml} --folder {input.folder} --output {output.tsv}"

rule mask_ontology:
    input:
        script=f"{FOLDER}/scripts/mask_ontology.py",
        tsv=rules.table_ontology.output.tsv,
        fasta_folder=FASTA_FOLDER
    output:
        tsv=f"{FOLDER}/data_processed/Mask/Ontology-{ontology_subset}.tsv.gz"
    shell:
        "python3 {input.script} --ontology_tsv {input.tsv} --ontology_key {ontology_subset} --fasta_folder {input.fasta_folder} --output {output.tsv} "

rule mask_adaptation:
    input:
        script=f"{FOLDER}/scripts/mask_adaptation.py",
        exp_folder=f"{FOLDER}/data_div/Experiments"
    output:
        tsv=f"{FOLDER}/data_processed/Mask/adaptation.tsv.gz"
    shell:
        "python3 {input.script} --exp_folder {input.exp_folder} --output {output.tsv}"

rule mask_identity_pct:
    input:
        script=f"{FOLDER}/scripts/mask_identity_pct.py",
        fasta_folder=FASTA_FOLDER
    output:
        tsv=f"{FOLDER}/data_processed/Mask/identity.{{identity}}.tsv.gz"
    shell:
        "python3 {input.script} --identity {wildcards.identity} --fasta_folder {input.fasta_folder} --output {output.tsv}"


rule mask_unaligned_flanks:
    input:
        script=f"{FOLDER}/scripts/mask_unaligned_flanks.py",
        fasta_folder=FASTA_FOLDER,
        tree_folder=TREE_FOLDER,
        fasta=rules.fixed_snp_by_pop.output.fasta
    output:
        tsv=f"{FOLDER}/data_processed/Mask/Flanks{{flank}}.{{species}}.{{popu}}.tsv.gz"
    shell:
        "python3 {input.script} --flank {wildcards.flank} --species {wildcards.species} --tree_folder {input.tree_folder} --fasta {input.fasta} --fasta_folder {input.fasta_folder} --output {output.tsv}"


def mask_list(species, popu):
    out = MASK_LIST.copy()
    if ("mask_unaligned_flanks" in config) and (config['mask_unaligned_flanks'] > 0):
        out += [f"{FOLDER}/data_processed/Mask/Flanks{config['mask_unaligned_flanks']}.{species}.{popu}.tsv.gz"]
    return out


def format_mask_list(m):
    fmt_str = f"--mask_CpG " if ("mask_CpG" in config and config['mask_CpG']) else ""
    return fmt_str + f"--mask {' '.join(m)}" if len(m) > 0 else ""


rule annotate_vcf:
    input:
        vcf=f"{FOLDER}/data_poly/{{species}}.{{popu}}.vcf.gz",
        script=f"{FOLDER}/scripts/annotate_vcf.py",
        sift_file=rules.merge_SIFT.output,
        mask=lambda wildcards: mask_list(wildcards.species,wildcards.popu),
        fasta_pop=rules.fixed_snp_by_pop.output.fasta,
    output:
        tsv=f"{FOLDER}/data_processed/vcf_annotate_{binning}/snps.{{species}}.{{popu}}.tsv.gz",
        bounds=f"{FOLDER}/data_processed/vcf_annotate_{binning}/bounds.{{species}}.{{popu}}.tsv"
    params:
        subsample=f"--subsample {config['subsample']}",
        anc_proba=f"--anc_proba {config['mask_anc_proba_SNPS'] if 'mask_anc_proba_SNPS' in config else 0.0}",
        mask=lambda wildcards: format_mask_list(mask_list(wildcards.species,wildcards.popu))
    shell:
        "python3 {input.script} {params.subsample} {params.mask} {params.anc_proba} --sift_file {input.sift_file} --vcf {input.vcf} --bins {bins} --windows {windows} --output_tsv {output.tsv} --fasta_pop {input.fasta_pop} --output_bounds {output.bounds}"


rule gather_bounds:
    input:
        script=f"{FOLDER}/scripts/gather_bounds.py",
        tsv=[f"{FOLDER}/data_processed/vcf_annotate_{binning}/bounds.{species}.{popu}.tsv" for popu, species in
             pop2sp.items()],
        sample_list=sample_list
    output:
        tsv=f"{FOLDER}/data_processed/merged_bounds/{binning}.tsv"
    shell:
        "python3 {input.script} --tsv {input.tsv} --sample_list {input.sample_list} --output {output.tsv}"

rule mask_unconserved:
    input:
        script=f"{FOLDER}/scripts/mask_unconserved.py",
        fasta_folder=FASTA_FOLDER
    output:
        tsv=f"{FOLDER}/data_processed/Mask/unconserved.tsv.gz"
    params:
        species=" ".join(sorted(set(pop2sp.values())))
    shell:
        "python3 {input.script} --species {params.species}  --fasta_folder {input.fasta_folder} --output {output.tsv}"

rule opportunities:
    input:
        script=f"{FOLDER}/scripts/genome_opportunities.py",
        exp_folder=f"{FOLDER}/data_div/Experiments",
        sift_folder=rules.SIFT_annotate.output,
        fasta_pop=rules.fixed_snp_by_pop.output.fasta,
        bounds=rules.annotate_vcf.output.bounds,
        unconserved=rules.mask_unconserved.output.tsv,
        mask=lambda wildcards: mask_list(wildcards.species,wildcards.popu)
    output:
        tsv=f"{FOLDER}/data_processed/opportunities_{binning}/DFE.{{species}}.{{popu}}.{{method}}.tsv"
    params:
        k=lambda wildcards: f"--subsample_genes {config['subsample_genes']}",
        mask=lambda wildcards: format_mask_list(mask_list(wildcards.species,wildcards.popu))
    shell:
        "python3 {input.script} {params.mask} --unconserved {input.unconserved} --bins {bins} --windows {windows} --bounds {input.bounds} {params.k} --method {wildcards.method} --exp_folder {input.exp_folder} --sift_folder {input.sift_folder} --fasta_pop {input.fasta_pop} --output {output.tsv}"

rule gather_opportunities:
    input:
        script=f"{FOLDER}/scripts/gather_opportunities.py",
        tsv=[f"{FOLDER}/data_processed/opportunities_{binning}/DFE.{species}.{popu}.MutSel.tsv" for popu, species in
             pop2sp.items()],
        sample_list=sample_list
    output:
        tsv=f"{FOLDER}/data_processed/merged_opportunities/{binning}.tsv"
    shell:
        "python3 {input.script} --tsv {input.tsv} --sample_list {input.sample_list} --output {output.tsv}"

rule ancestral_reconstruction:
    input:
        script=f"{FOLDER}/scripts/ancestral_reconstruction.py",
        fasta_folder=FASTA_FOLDER,
        tree_folder=TREE_FOLDER,
        fasta_pop=rules.fixed_snp_by_pop.output.fasta,
        exec=f"{FOLDER}/utils/FastML.v3.11/programs/fastml/fastml"
    output:
        touch(f"{FOLDER}/data_processed/ancestral_reconstruction/{{species}}.{{popu}}.touch")
    params:
        folder=f"{FOLDER}/data_processed/ancestral_reconstruction/{{species}}.{{popu}}",
        k=lambda wildcards: f"--subsample_genes {config['subsample_genes_ancestral']}"
    shell:
        "python3 {input.script} --exec {input.exec} {params.k} --species {wildcards.species} --fasta_folder {input.fasta_folder} --tree_folder {input.tree_folder} --fasta_pop {input.fasta_pop} --output {params.folder}"

rule substitutions_mapping:
    input:
        script=f"{FOLDER}/scripts/substitutions_mapping.py",
        exp_folder=f"{FOLDER}/data_div/Experiments",
        ancestral=rules.ancestral_reconstruction.output,
        bounds=rules.annotate_vcf.output.bounds,
        mask=lambda wildcards: mask_list(wildcards.species,wildcards.popu),
        opp=f"{FOLDER}/data_processed/opportunities_{binning}/DFE.{{species}}.{{popu}}.MutSel.tsv"
    output:
        tsv=f"{FOLDER}/data_processed/ancestral_{binning}/{{species}}.{{popu}}.tsv"
    params:
        folder=f"{FOLDER}/data_processed/ancestral_reconstruction/{{species}}.{{popu}}",
        anc_proba=f"--anc_proba {config['mask_anc_proba_substitutions'] if 'mask_anc_proba_substitutions' in config else 0.0}",
        mask=lambda wildcards: format_mask_list(mask_list(wildcards.species,wildcards.popu))
    shell:
        "python3 {input.script} {params.mask} {params.anc_proba} --opp {input.opp} --species {wildcards.species} --bins {bins} --windows {windows} --bounds {input.bounds} --exp_folder {input.exp_folder} --ancestral {params.folder} --output {output.tsv}"

rule substitutions:
    input:
        script=f"{FOLDER}/scripts/substitutions.py",
        exp_folder=f"{FOLDER}/data_div/Experiments",
        xml_folder=f"{FOLDER}/data_div/omm_markers",
        ancestral=rules.ancestral_reconstruction.output
    output:
        tsv=f"{FOLDER}/TSV/subs.{{species}}.{{popu}}.tsv.gz"
    params:
        folder=f"{FOLDER}/data_processed/ancestral_reconstruction/{{species}}.{{popu}}"
    shell:
        "python3 {input.script} --threshold 0.0 --species {wildcards.species} --exp_folder {input.exp_folder} --xml_folder {input.xml_folder} --ancestral {params.folder} --output {output.tsv}"


rule polymorphism:
    input:
        script=f"{FOLDER}/scripts/polymorphism.py",
        xml_folder=f"{FOLDER}/data_div/omm_markers",
        vcf=f"{FOLDER}/data_poly/{{species}}.{{popu}}.vcf.gz"
    output:
        tsv=f"{FOLDER}/TSV/poly.{{species}}.{{popu}}.tsv.gz"
    params:
        subsample=f"--subsample {config['subsample']}",
        anc_proba=f"--anc_proba {config['mask_anc_proba_SNPS'] if 'mask_anc_proba_SNPS' in config else 0.0}"
    shell:
        "python3 {input.script} --species {wildcards.species} --xml_folder {input.xml_folder} {params.anc_proba} --vcf {input.vcf} --output_tsv {output.tsv}"


rule plot_variations:
    input:
        script=f"{FOLDER}/scripts/plot_variations.py",
        tsv=f"{FOLDER}/TSV/{{variations}}.{{species}}.{{popu}}.tsv.gz",
        tree=f"{FOLDER}/data_div/mammals_species.nwk",
        fasta_folder=FASTA_FOLDER,
        mask=lambda wildcards: mask_list(wildcards.species,wildcards.popu),
        bounds=rules.annotate_vcf.output.bounds
    output:
        pdf=f"{FOLDER}/TSV/{{variations}}.{{species}}.{{popu}}.{binning}.pdf"
    params:
        mask=lambda wildcards: format_mask_list(mask_list(wildcards.species,wildcards.popu))
    shell:
        "python3 {input.script} {params.mask} --species {wildcards.species} --tree {input.tree} --fasta_folder {input.fasta_folder} --input {input.tsv} --output {output.pdf} --bins {bins} --windows {windows} --bounds {input.bounds}"


def reconstruct_substitutions(wildcards):
    return config['ALPHA'] and wildcards.method == "MutSel"


rule sfs:
    input:
        script=f"{FOLDER}/scripts/plot_sfs.py",
        tsv=rules.annotate_vcf.output.tsv,
        opportunities=rules.opportunities.output.tsv,
        bounds=rules.annotate_vcf.output.bounds,
        subs=lambda wildcards: rules.substitutions_mapping.output.tsv if reconstruct_substitutions(wildcards) else []
    output:
        dir=directory(f"{EXP_FOLDER}/analysis/{{species}}.{{popu}}.{{method}}"),
        pdf=f"{EXP_FOLDER}/analysis/{{species}}.{{popu}}.{{method}}-sfs.pdf",
        tsv=f"{EXP_FOLDER}/analysis/{{species}}.{{popu}}.{{method}}-sfs-summary-stats.tsv"
    params:
        config=f"--nbr_replicates {config['nbr_replicates']} --subsample {config['subsample']}",
        substitutions=lambda wildcards, input, output: f"--substitutions {input.subs}" if reconstruct_substitutions(wildcards) else ""
    shell:
        "python3 {input.script} {params.substitutions} --bins {bins} --windows {windows} --bounds {input.bounds} {params.config} --tsv {input.tsv} --pop {wildcards.popu} --method {wildcards.method} --opportunities {input.opportunities} --output_tsv {output.tsv} --output_pdf {output.pdf} --output_dir {output.dir}"


rule polyDFE:
    input:
        sfs=rules.sfs.output.dir,
        polyDFE=f"{FOLDER}/utils/polyDFE/polyDFE-2.0-macOS-64-bit",
        init_file=f"{FOLDER}/config/{model}_init.tsv",
        range_file=f"{FOLDER}/config/{model}_range.tsv"
    output:
        out=f"{EXP_FOLDER}/analysis/{{species}}.{{popu}}.{{method}}/{{cat}}.{model}.out",
        stderr=f"{EXP_FOLDER}/analysis/{{species}}.{{popu}}.{{method}}/{{cat}}.{model}.stderr"
    params:
        sfs=lambda wildcards, input, output: os.path.join(input.sfs,f"{wildcards.cat}.sfs")
    shell:
        "{input.polyDFE} -d {params.sfs} -i {input.init_file} 1 -r {input.range_file} 1 {polyDFE_param} 1> {output.out} 2> {output.stderr}"


rule parse_results_DFE:
    input:
        script=f"{FOLDER}/scripts/parse_results_DFE.py",
        postprocessing=f"{FOLDER}/scripts/postprocessing.R",
        modelfile=lambda wildcards: expand(rules.polyDFE.output.out,species=wildcards.species,popu=wildcards.popu,
            method=wildcards.method,cat=method_cats[wildcards.method]),
        substitutions=rules.substitutions_mapping.output.tsv,
        bounds=rules.annotate_vcf.output.bounds
    output:
        pdf=f"{EXP_FOLDER}/analysis/{{species}}.{{popu}}.{{method}}.{model}.pdf",
        tsv=f"{EXP_FOLDER}/analysis/{{species}}.{{popu}}.{{method}}.{model}.tsv"
    shell:
        "python3 {input.script} --postprocessing {input.postprocessing} --substitutions {input.substitutions} --bins {bins} --windows {windows} --bounds {input.bounds} --input {input.modelfile} --method {wildcards.method} --output {output.pdf}"

rule plot_dfe_heatmap:
    input:
        script=f"{FOLDER}/scripts/plot_heatmap.py",
        tsv_theta=map(lambda p: f"{p}-sfs-summary-stats.tsv",SFS_LIST),
        tsv_dfe=map(lambda p: f"{p}.{model}.tsv",SFS_LIST),
        tsv_mut_rate=f"{FOLDER}/config/mutation_rates.tsv",
        sample_list=sample_list
    output:
        tsv=f"{EXP_FOLDER}/results/Theta.results.tsv"
    shell:
        "python3 {input.script} --tsv_theta {input.tsv_theta} --tsv_dfe {input.tsv_dfe} --tsv_mut_rate {input.tsv_mut_rate} --sample_list {input.sample_list} --bins {bins} --windows {windows} --output {output.tsv}"

rule plot_multiple_regression:
    input:
        script=f"{FOLDER}/scripts/plot_multiple_regression.py",
        tsv=map(lambda p: f"{p}.{model}.tsv",SFS_LIST),
        sample_list=sample_list
    output:
        tsv=f"{EXP_FOLDER}/results/regression.results.tsv"
    shell:
        "python3 {input.script} --tsv {input.tsv} --sample_list {input.sample_list} --output {output.tsv}"

rule plot_regression:
    input:
        script=f"{FOLDER}/scripts/plot_regression.py",
        pgls_script=f"{FOLDER}/scripts/PGLS.R",
        pgls_tree=f"{FOLDER}/data_processed/trimmed_tree_{config['SAMPLE_LIST']}.tree",
        tsv=[rules.gather_opportunities.output.tsv, rules.gather_bounds.output.tsv] + (
            [rules.plot_multiple_regression.output.tsv] if (bins > 10 and windows != 0) else [
                rules.plot_dfe_heatmap.output.tsv]),
        sample_list=sample_list
    output:
        tsv=f"{EXP_FOLDER}/regression-{{method}}/results.tsv",
        pgls_tsv=f"{EXP_FOLDER}/regression-{{method}}/PGLS.results.tsv"
    shell:
        "python3 {input.script} --tsv {input.tsv} --method {wildcards.method} --sample_list {input.sample_list} --bins {bins} --windows {windows} --output {output.tsv} --pgls_script {input.pgls_script} --pgls_tree {input.pgls_tree} --pgls_output {output.pgls_tsv}"


rule upset_plot:
    input:
        vcf=VCF_LIST,
        script=f"{FOLDER}/scripts/plot_venn.py",
        mask=MASK_LIST
    output:
        pdf=f"{FOLDER}/data_processed/upset_plot/{binning}.pdf"
    params:
        mask=lambda wildcards: format_mask_list(MASK_LIST)
    shell:
        "python3 {input.script} {params.mask} --vcf {input.vcf} --output {output.pdf}"

rule SNP_effect:
    input:
        script=f"{FOLDER}/scripts/SNP_effect.py",
        tex=f"{FOLDER}/scripts/main-table.tex",
        vcf=f"{FOLDER}/data_poly/Homo_sapiens.EUR.vcf.gz",
        effect=f"{FOLDER}/data_phenotype/v92/homo_sapiens_{{trait}}_associated.vcf.gz",
        mask=mask_list("Homo_sapiens","EUR")
    output:
        pdf=f"{FOLDER}/data_processed/SNP_effect/{binning}_{{trait}}/output.tsv"
    params:
        mask=format_mask_list(mask_list("Homo_sapiens","EUR"))
    shell:
        "python3 {input.script} {params.mask} --tex {input.tex} --vcf {input.vcf} --effect {input.effect} --output {output.pdf}"


rule latex:
    input:
        script=f"{FOLDER}/scripts/tex_sfs.py",
        supp_files=LIST_SUPP,
        sfs=map(lambda p: f"{p}-sfs.pdf",SFS_LIST),
        tex_source=f"{FOLDER}/scripts/supp-mat.tex",
        tsv_dfe=rules.plot_dfe_heatmap.output.tsv,
        tsv_reg=expand(rules.plot_regression.output.tsv,method=config['METHOD_LIST']),
        sample_list=sample_list
    output:
        tex_include=f"{EXP_FOLDER}/include-figures.tex",
        tex_target=f"{EXP_FOLDER}/supp-mat.tex",
        pdf=f"{EXP_FOLDER}/supp-mat.pdf"
    params:
        hist_prefix="--hist_prefix " + (
            f"{FOLDER}/TSV/HOLD_{binning}" if output_supp else f"{FOLDER}/data_processed/ancestral_{binning}/"),
        dfe_prefix=f"--dfe_prefix {FOLDER}/data_processed/opportunities_{binning}/DFE."
    shell: "python3 {input.script} {params.hist_prefix} {params.dfe_prefix} --sample_list {input.sample_list} --sfs {input.sfs} --results {input.tsv_dfe} --tex_source {input.tex_source} --tex_target {output.tex_target} --tex_include {output.tex_include}"
