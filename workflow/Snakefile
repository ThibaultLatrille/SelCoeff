import os
import sys
import pandas as pd


configfile: 'config/config.yaml'

FOLDER = os.path.abspath('.')
sys.path.append('.')
from scripts.libraries import CategorySNP

sample_list = f"{FOLDER}/config/{config['SAMPLE_LIST']}"
bins = config["bins"]
pop2sp = {}
SFS_LIST = []

for id, row in list(pd.read_csv(sample_list,sep='\t').iterrows()):
    pop2sp[row['SampleName']] = row['Species']
    for method in config['METHOD_LIST']:
        if method == 'SIFT' and row['Species'] not in config['SIFT']:
            continue
        SFS_LIST.append(FOLDER + f"/analysis/{row['Species']}.{row['SampleName']}.{method}")

ruleorder: parse_results_DFE > sfs

rule all:
    input:
        f'{FOLDER}/manuscript/main.pdf',
        expand("{path}.{model}.pdf",path=SFS_LIST,model=config['MODEL_LIST'])

rule download:
    output: FOLDER + '/data_poly/dl.{species}.SIFT.vcf.gz'
    params: lambda wildcards: config["SIFT"][wildcards.species]
    shell:
        "wget -c {params} -O {output}"

rule opportunities:
    input:
        script=f'{FOLDER}/scripts/genome_opportunities.py',
        exp_folder=f'{FOLDER}/data_div/Experiments',
        fasta_folder=f'{FOLDER}/data_div/omm_NT_fasta.v10c_116'
    output:
        opportunities=FOLDER + '/data_div/opportunities.{method}.tsv'
    params:
        species=" ".join(set(pop2sp.values()))
    shell:
        'python3 {input.script} --species {params.species} --method {wildcards.method} --exp_folder {input.exp_folder} --fasta_folder {input.fasta_folder} --output {output.opportunities}'

rule SIFT_intersect:
    input:
        vcf=FOLDER + '/data_poly/{species}.{popu}.vcf.gz',
        vcf_sift=rules.download.output
    output:
        vcf=FOLDER + '/data_poly/{species}.{popu}.SIFT.vcf.gz'
    shell:
        'bedtools intersect -a {input.vcf_sift} -b {input.vcf} -wb -wa -header | gzip > {output.vcf}'

rule sfs:
    input:
        vcf=lambda wildcards: rules.SIFT_intersect.output.vcf if wildcards.method == 'SIFT' else FOLDER + '/data_poly/{species}.{popu}.vcf.gz',
        script=f'{FOLDER}/scripts/plot_sfs.py',
        genome_results=f'{FOLDER}/data_div/genome_results.tsv',
        opportunities=FOLDER + '/data_div/opportunities.{method}.tsv'
    output:
        pdf=FOLDER + '/analysis/{species}.{popu}.{method}.pdf',
        tsv=FOLDER + '/analysis/{species}.{popu}.{method}.tsv'
    params:
        lambda wildcards: f'--nbr_replicates {config["nbr_replicates"]} --subsample {config["subsample"]} --species {pop2sp[wildcards.popu]}'
    shell:
        'python3 {input.script} {params} --vcf {input.vcf} --pop {wildcards.popu} --method {wildcards.method} --bins {bins} --genome_results {input.genome_results} --opportunities {input.opportunities} --output {output.pdf}'

rule polyDFE:
    input:
        sfs=rules.sfs.output.pdf,
        polyDFE=f'{FOLDER}/utils/polyDFE/polyDFE-2.0-linux-64-bit',
        init_file=FOLDER + '/config/{model}_init.txt',
        range_file=FOLDER + '/config/{model}_range.txt'
    output:
        out=FOLDER + '/analysis/{species}.{popu}.{method}.{cat}.{model}.out',
        stderr=FOLDER + '/analysis/{species}.{popu}.{method}.{cat}.{model}.stderr'
    params:
        sfs=FOLDER + '/analysis/{species}.{popu}.{method}.{cat}.sfs',
        model=lambda wildcards: "-m D 5" if wildcards.model == "polyDFE_D" else "-m C"
    shell:
        '{input.polyDFE} -d {params.sfs} -i {input.init_file} 1 -r {input.range_file} 1 -w {params.model} 1> {output.out} 2> {output.stderr}'

rule grapes:
    input:
        sfs=rules.sfs.output.pdf,
        grapes=f'{FOLDER}/utils/grapes/grapes/grapes',
    output:
        out=FOLDER + '/analysis/{species}.{popu}.{method}.{cat}.grapes.out',
        stdout=FOLDER + '/analysis/{species}.{popu}.{method}.{cat}.grapes.stdout',
        stderr=FOLDER + '/analysis/{species}.{popu}.{method}.{cat}.grapes.stderr'
    params:
        dofe=FOLDER + '/analysis/{species}.{popu}.{method}.{cat}.dofe',
    shell:
        "{input.grapes} -in {params.dofe} -out {output.out} -model GammaExpo -no_div_data 1> {output.stdout} 2> {output.stderr}"

rule parse_results_DFE:
    input:
        modelfile=lambda wildcards: expand(FOLDER + '/analysis/{{species}}.{{popu}}.{{method}}.{cat}.{{model}}.out',cat=CategorySNP(wildcards.method,bins).all()),
        script=f'{FOLDER}/scripts/parse_results_DFE.py'
    output:
        pdf=FOLDER + '/analysis/{species}.{popu}.{method}.{model}.pdf',
        tsv=FOLDER + '/analysis/{species}.{popu}.{method}.{model}.tsv'
    shell:
        'python3 {input.script} --input {input.modelfile} --method {wildcards.method} --bins {bins} --output {output.pdf}'

rule plot_theta_heatmap:
    input:
        tsv=map(lambda p: f"{p}.tsv",SFS_LIST),
        sample_list=sample_list,
        script=f'{FOLDER}/scripts/plot_heatmap.py'
    output:
        tsv=f'{FOLDER}/results/Theta.results.tsv'
    shell:
        'python3 {input.script} --tsv {input.tsv} --sample_list {input.sample_list} --bins {bins} --output {output.tsv}'

rule plot_dfe_heatmap:
    input:
        tsv=map(lambda p: f"{p}.polyDFE_D.tsv",SFS_LIST),
        sample_list=sample_list,
        script=f'{FOLDER}/scripts/plot_heatmap.py'
    output:
        tsv=f'{FOLDER}/results/DFE.results.tsv'
    shell:
        'python3 {input.script} --tsv {input.tsv} --sample_list {input.sample_list} --bins {bins} --output {output.tsv}'


rule latex:
    input:
        sfs=map(lambda p: f"{p}.pdf",SFS_LIST),
        tex=f'{FOLDER}/manuscript/main.tex',
        tsv_theta=f'{FOLDER}/results/Theta.results.tsv',
        tsv_dfe=f'{FOLDER}/results/DFE.results.tsv',
        script=f'{FOLDER}/scripts/tex_sfs.py'
    output:
        tex=FOLDER + '/manuscript/include-figures.tex',
        pdf=FOLDER + '/manuscript/main.pdf'
    shell: 'python3 {input.script} --sfs {input.sfs} --tex_doc {input.tex} --results {input.tsv_theta} --tex_include {output.tex}'
